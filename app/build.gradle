apply plugin: 'com.android.application'

Properties properties = new Properties()
properties.load(project.rootProject.file('keystore.properties').newDataInputStream())

def CLIENT_ID = properties.getProperty("CLIENT_ID")
def ADMOB_APP_ID = properties.getProperty("ADMOB_APP_ID")
def ADMOB_APP_UNIT_ID = properties.getProperty("ADMOB_APP_UNIT_ID")

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.sk7software.spotifyexplicittrackskipper"
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 2
        versionName "0.1b"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes.each {
        it.buildConfigField('String', 'CLIENT_ID', CLIENT_ID)
        it.resValue 'string', 'ADMOB_APP_ID', ADMOB_APP_ID
        it.resValue 'string', 'ADMOB_APP_UNIT_ID', ADMOB_APP_UNIT_ID
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "tier"

    productFlavors {
        lite {
            applicationIdSuffix ".lite"
            versionNameSuffix "-lite"
        }
        full {
            applicationIdSuffix ".full"
            versionNameSuffix "-full"
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    packagingOptions {
        pickFirst 'META-INF/LICENSE'
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.spotify.android:auth:1.0.0-alpha'
    api 'com.android.support:appcompat-v7:28.0.0'
    api 'com.android.support:recyclerview-v7:28.0.0'
    api 'com.android.support:design:28.0.0'
    api 'com.android.support:customtabs:28.0.0'
    api 'com.android.support:support-v4:28.0.0'
    api 'com.android.support:mediarouter-v7:28.0.0'
    implementation 'com.android.volley:volley:1.1.0'
    implementation 'com.android.support.constraint:constraint-layout:2.0.0-alpha3'
    implementation 'com.google.android.gms:play-services-ads:17.1.3'
    implementation 'com.google.firebase:firebase-core:16.0.7'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.8.9'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.8.9'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.8.9'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.json:json:20160810'
    testImplementation 'org.robolectric:robolectric:3.3.2'
    testImplementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.5'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.8.0'
}

apply plugin: 'com.google.gms.google-services'
